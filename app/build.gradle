plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    id 'org.tboox.gradle-xmake-plugin' version '1.2.3'
    id 'com.google.gms.google-services'
}

def getGitTag() {
    def tag = "unknown"
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--abbrev=0'
            standardOutput = stdout
        }
        tag = stdout.toString().trim()
        if (tag.startsWith("v")) {
            tag = tag.substring(1)
        }
    } catch (ignored) {
    }
    return tag
}

static def getVersionCodeByTimestamp() {
    def baseDate = Date.parse("yyyy-MM-dd HH:mm:ss", "2025-01-01 00:00:00")
    def now = new Date()
    def diff = (now.time - baseDate.time) / 1000
    return diff.toInteger()
}

android {
    namespace 'org.levimc.launcher'
    compileSdk 36
    defaultConfig {
        applicationId "org.levimc.launcher"
        minSdk 24
        targetSdk 36
        versionCode getVersionCodeByTimestamp()
        versionName "1.1.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
        xmake {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }

    buildFeatures {
        viewBinding true
    }
    buildFeatures {
        prefab true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }
    externalNativeBuild {
        xmake {
            path "src/main/cpp/leviutils/xmake.lua"
            logLevel "verbose"
            buildMode "release"
        }
    }
    ndkVersion "25.2.9519653"
}

dependencies {
    implementation project(':preloader')
    implementation libs.core.splashscreen
    implementation libs.apk.parser
    implementation libs.appcompat
    implementation libs.conscrypt.android
    implementation libs.constraintlayout
    implementation libs.material
    implementation libs.gson
    implementation libs.okhttp3.okhttp
    implementation libs.activity
    implementation libs.firebase.firestore
    implementation libs.navigation.fragment
    implementation libs.navigation.ui
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    implementation platform('com.google.firebase:firebase-bom:34.3.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.android.gms:play-services-ads:24.7.0'
}
